/*
    == SOUL example code ==
    == Author: Ivan Cohen ==
*/

/// Title: Diode Clipper example
///
/// This is an algorithm simulating a Diode Clipper, which is an analog low-pass
/// filter with a resistance, a capacitance, and two parallel diodes put in
/// opposite directions. The linear part is simulated using the TPT structure,
/// and the nonlinear part uses the Newton-Raphson's method to solve the roots
/// of the implicit equation.

graph Diode  [[ main ]]
{
    input  stream float audioIn;    // just using mono in and out
    output stream float audioOut;

    input event float cutoffFrequency   [[ name: "Cutoff", min: 20, max: 20000, init: 10000, step: 10  ]];
    input event float gaindB            [[ name: "Gain",   min: 0,  max:    40, init:    20, step: 0.1 ]];

    let diodeClipper = DiodeClipper * 4;  // oversampling 4 times

    connection
    {
        audioIn         -> diodeClipper.audioIn;
        cutoffFrequency -> diodeClipper.cutoffFrequencyIn;
        gaindB          -> diodeClipper.gaindBIn;

        diodeClipper -> audioOut;
    }
}

//==============================================================================
processor DiodeClipper
{
    input  stream float audioIn;
    output stream float audioOut;

    input event float cutoffFrequencyIn;
    input event float gaindBIn;

    event cutoffFrequencyIn (float f)   { cutoffFrequency = f; }
    event gaindBIn (float f)            { gaindB = f; }

    // Diode Clipper parameters
    float cutoffFrequency = 10000.0f;
    float gaindB = 40.0f;

    // filter variables
    float G, gain;

    // Main processing function
    void run()
    {
        // internal constants (1N4148)
        let Is          = 2.52e-9;
        let mu          = 1.752;
        let Vt          = 26e-3;
        let R           = 2.2e3;
        let tolerance   = 1e-12;

        // state variables
        float32 s1;
        float64 out;

        let updateInterval = 8;   // number of samples between calls to updateFilterVariables()
        let maxNewtonRaphsonIterations = 64;

        loop
        {
            updateFilterVariables();

            // DAFX15 Capped Step
            let deltaLim = mu * Vt * acosh (mu * Vt / 2.0 / (R * Is * G));

            loop (updateInterval)
            {
                let in = audioIn * gain;
                let p = G * (in - s1) + s1;
                float64 delta = 1e9;

                loop (maxNewtonRaphsonIterations)
                {
                    if (abs (delta) <= tolerance)
                        break;

                    let J = p - (2 * G * R * Is) * sinh (out / (mu * Vt)) - out;
                    let dJ = -1 - G * 2 * R * Is / (mu * Vt) * cosh (out / (mu * Vt));

                    // DAFX15 Capped Step
                    delta = clamp (-J / dJ, -deltaLim, deltaLim);

                    // next iteration
                    out += delta;
                }

                // TPT structure updates
                let v = float (out) - s1;
                s1 = float (out) + v;

                audioOut << float (out);
                advance();
            }
        }
    }

    void updateFilterVariables()
    {
        // low-pass filter internal variables update
        let cutoff = clamp (cutoffFrequency, 10.0f, float32 (processor.frequency) * 0.49999f);
        let g = tan (float (pi) * cutoff / float32 (processor.frequency));
        G = g / (1 + g);

        // gain update
        gain = soul::dBtoGain (gaindB);
    }
}
